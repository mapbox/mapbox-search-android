plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'kotlin-parcelize'
    id 'kotlin-kapt'
    id 'de.mannodermaus.android-junit5'
    id 'com.mapbox.android.sdk.versions'
    id 'com.getkeepsafe.dexcount'
}

apply from: "../gradle/ktlint.gradle"
apply from: "../gradle/versions.gradle"
//apply from: "../gradle/pitest.gradle"
apply from: "../gradle/jacoco.gradle"
apply from: "../gradle/dexcount.gradle"
apply from: "../gradle/dokka.gradle"
apply from: "../gradle/publish.gradle"
apply from: "../gradle/license.gradle"
apply from: "../gradle/track-public-apis.gradle"

android {
    namespace 'com.mapbox.search.offline'
    compileSdkVersion androidVersions.compileSdkVersion

    defaultConfig {
        minSdkVersion androidVersions.minSdkVersion
        targetSdkVersion androidVersions.targetSdkVersion

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        debug {
            testCoverageEnabled project.hasProperty('coverage')
            consumerProguardFiles "${projectDir}/proguard/consumer-proguard-offline.txt"
        }
        release {
            jniDebuggable false
            minifyEnabled false
            consumerProguardFiles "${projectDir}/proguard/consumer-proguard-offline.txt"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildFeatures {
        buildConfig = true
    }

    kotlinOptions {
        allWarningsAsErrors = !project.hasProperty('android.injected.invoked.from.ide')
        jvmTarget = JavaVersion.VERSION_1_8.toString()

        /**
         * Migrate to Groovy syntax when the issue https://youtrack.jetbrains.com/issue/KT-37652 is resolved
         *
         *     kotlin {
         *         explicitApi()
         *     }
         */
        freeCompilerArgs += [
                '-Xexplicit-api=strict',
                '-Xopt-in=com.mapbox.search.common.RestrictedMapboxSearchAPI'
        ]
    }

    testOptions {
        unitTests {
            returnDefaultValues = true
        }
    }

    lint {
        abortOnError true
        disable 'ParcelCreator'
    }
}

dependencies {
    implementation project(path: ':base')
    api project(path: ':sdk-common')

    implementation dependenciesList.kotlinStdlibJdk8

    implementation dependenciesList.androidxAnnotation
    implementation dependenciesList.androidxCollection
    implementation dependenciesList.androidxCore
    implementation dependenciesList.androidxStartupRuntime

    api dependenciesList.mapboxCommon

    implementation dependenciesList.mapboxBase

    implementation 'com.mapbox.mapboxsdk:mapbox-sdk-turf:6.15.0'

    ktlint dependenciesList.ktlint

    testImplementation dependenciesList.mapboxTestDsl
    testImplementation dependenciesList.gmazzoOkHttpMock
    testImplementation dependenciesList.junit
    testImplementation dependenciesList.mockitoCore

    testImplementation dependenciesList.mockk
    // The following libraries were added for preventing
    // Pitest from crash due to missing libraries in passed
    // classpath.
    testImplementation dependenciesList.mockkCommon
    testImplementation dependenciesList.mockkApi
    testImplementation dependenciesList.mockkJvm
    testImplementation dependenciesList.kotlinReflect

    testImplementation dependenciesList.jupiterApi
    testImplementation dependenciesList.jupiterEngine
    testImplementation dependenciesList.jupiterParams
    testImplementation project(":common-tests")

    androidTestImplementation dependenciesList.androidxTestRunner
    androidTestImplementation dependenciesList.androidxJunit
    androidTestImplementation dependenciesList.androidXEspressoCore
    androidTestImplementation dependenciesList.okHttpMockWebServer
    androidTestImplementation project(":common-tests")
}
