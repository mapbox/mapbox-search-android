buildscript {
    apply from: "${rootDir}/gradle/versions.gradle"

    def sdkRegistryToken = project.properties['SDK_REGISTRY_TOKEN'] ?: System.getenv('SDK_REGISTRY_TOKEN')
    if (sdkRegistryToken == null || sdkRegistryToken.empty) {
        throw new Exception("SDK Registry token is not specified.")
    }

    repositories {
        google()
        jcenter()

        maven {
            url 'https://api.mapbox.com/downloads/v2/releases/maven'
            authentication {
                basic(BasicAuthentication)
            }
            credentials {
                username = "mapbox"
                password = sdkRegistryToken
            }
        }
    }

    configurations.maybeCreate("pitest")

    dependencies {
        classpath "com.mapbox.mapboxsdk:mapbox-android-sdk-versions:1.1.0"
        classpath "com.android.tools.build:gradle:$gradle_plugin"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "de.mannodermaus.gradle.plugins:android-junit5:1.6.0.0"
        pitest "org.pitest:pitest-junit5-plugin:0.10"
        classpath "pl.droidsonroids.gradle:gradle-pitest-plugin:0.2.5"
        classpath "com.getkeepsafe.dexcount:dexcount-gradle-plugin:1.0.2"
        classpath "com.jaredsburrows:gradle-license-plugin:0.8.90"
        classpath 'com.mapbox.gradle.plugins:sdk-registry:0.4.0'
        classpath 'me.champeau.gradle:japicmp-gradle-plugin:0.2.8'
    }
}

plugins {
    id "io.gitlab.arturbosch.detekt" version "1.5.1"
    id "org.jetbrains.dokka" version "1.4.30"
    id "org.jetbrains.kotlinx.binary-compatibility-validator" version "0.4.0"
}

apiValidation {
    ignoredProjects += ["sample", "custom-detekt-rules", "common-tests", "sdk-common"]
}

def sdkRegistryToken = project.properties['SDK_REGISTRY_TOKEN'] ?: System.getenv('SDK_REGISTRY_TOKEN')
if (sdkRegistryToken == null || sdkRegistryToken.empty) {
    throw new Exception("SDK Registry token is not specified.")
}

allprojects {
    apply plugin: 'io.gitlab.arturbosch.detekt'

    detekt {
        config = files("${rootDir}/detekt-config.yml")
    }

    dependencies {
        detekt "io.gitlab.arturbosch.detekt:detekt-cli:$detekt_version"
        detekt project(":custom-detekt-rules")
    }

    repositories {
        google()
        jcenter()

        maven {
            url 'https://api.mapbox.com/downloads/v2/releases/maven'
            authentication {
                basic(BasicAuthentication)
            }
            credentials {
                username = "mapbox"
                password = sdkRegistryToken
            }
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

// Keeping `detektAll` here, because moving to another file causes a lot of problems
task detektAll(type: io.gitlab.arturbosch.detekt.Detekt) {
    description = "Runs over whole code base without the starting overhead for each module."
    parallel = true
    buildUponDefaultConfig = false
    source = files(rootDir)
    failFast = true
    config.setFrom(files("$rootDir/detekt-config.yml"))

    include("**/*.kt")
    include("**/*.kts")
    exclude("**/res/**")
    exclude("**/build/**")

    reports {
        html {
            enabled = true
            destination = file("${rootProject.buildDir}/reports/detekt.html")
        }
    }

    doFirst {
        source = subprojects
                .findAll { subproject -> !subproject.hasProperty('ignoreDetekt') || !subproject.ignoreDetekt }
                .collect { subproject -> subproject.projectDir }
    }
}

apply plugin: 'android-reporting'
