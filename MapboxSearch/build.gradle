buildscript {
    apply from: "${rootDir}/gradle/versions.gradle"

    def sdkRegistryToken = project.properties['SDK_REGISTRY_TOKEN'] ?: System.getenv('SDK_REGISTRY_TOKEN')
    if (sdkRegistryToken == null || sdkRegistryToken.empty) {
        throw new Exception("SDK Registry token is not specified.")
    }

    repositories {
        google()
        mavenCentral()

        maven {
            url 'https://api.mapbox.com/downloads/v2/releases/maven'
            authentication {
                basic(BasicAuthentication)
            }
            credentials {
                username = "mapbox"
                password = sdkRegistryToken
            }
        }
    }

    configurations.maybeCreate("pitest")

    dependencies {
        classpath pluginDependencies.mapboxSdkVersions
        classpath pluginDependencies.mapboxSdkRegistry
        classpath pluginDependencies.gradle
        classpath pluginDependencies.kotlin
        classpath pluginDependencies.androidJunit5
        pitest pluginDependencies.pitest
        classpath pluginDependencies.gradlePitest
        classpath pluginDependencies.licence
        classpath pluginDependencies.japicmp
    }
}

plugins {
    id "io.gitlab.arturbosch.detekt" version "1.17.0"
    id "org.jetbrains.dokka" version "1.7.20"
    id "org.jetbrains.kotlinx.binary-compatibility-validator" version "0.13.2"
    id "com.getkeepsafe.dexcount" version "3.0.1"
}

apiValidation {
    ignoredProjects += ["sample", "custom-detekt-rules", "common-tests", "base"]
}

def sdkRegistryToken = project.properties['SDK_REGISTRY_TOKEN'] ?: System.getenv('SDK_REGISTRY_TOKEN')
if (sdkRegistryToken == null || sdkRegistryToken.empty) {
    throw new Exception("SDK Registry token is not specified.")
}

allprojects {
    apply plugin: 'io.gitlab.arturbosch.detekt'

    detekt {
        config = files("${rootDir}/detekt-config.yml")
    }

    dependencies {
        detekt dependenciesList.detektCli
        detekt project(":custom-detekt-rules")
    }

    repositories {
        google()
        mavenCentral()

        maven {
            url 'https://api.mapbox.com/downloads/v2/releases/maven'
            authentication {
                basic(BasicAuthentication)
            }
            credentials {
                username = "mapbox"
                password = sdkRegistryToken
            }
        }

        maven {
            url 'https://api.mapbox.com/downloads/v2/snapshots/maven'
            authentication {
                basic(BasicAuthentication)
            }
            credentials {
                username = "mapbox"
                password = sdkRegistryToken
            }
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

// Keeping `detektAll` here, because moving to another file causes a lot of problems
task detektAll(type: io.gitlab.arturbosch.detekt.Detekt) {
    description = "Runs over whole code base without the starting overhead for each module."
    parallel = true
    buildUponDefaultConfig = false
    source = files(rootDir)
    failFast = true
    config.setFrom(files("$rootDir/detekt-config.yml"))

    include("**/*.kt")
    include("**/*.kts")
    exclude("**/res/**")
    exclude("**/build/**")

    reports {
        html {
            enabled = true
            destination = file("${rootProject.buildDir}/reports/detekt.html")
        }
    }

    doFirst {
        source = subprojects
                .findAll { subproject -> !subproject.hasProperty('ignoreDetekt') || !subproject.ignoreDetekt }
                .collect { subproject -> subproject.projectDir }
    }
}

apply plugin: 'android-reporting'
