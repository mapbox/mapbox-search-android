plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'kotlin-parcelize'
    id 'kotlin-kapt'
    id 'com.mapbox.android.sdk.versions'
    id 'de.mannodermaus.android-junit5'
    id 'com.getkeepsafe.dexcount'
}

apply from: "../gradle/ktlint.gradle"
apply from: "../gradle/jacoco.gradle"
apply from: "../gradle/versions.gradle"
apply from: "../gradle/dexcount.gradle"
apply from: "../gradle/dokka.gradle"
apply from: "../gradle/publish.gradle"
apply from: "../gradle/license.gradle"
apply from: "../gradle/track-public-apis.gradle"

android {
    namespace 'com.mapbox.search.autocomplete'
    compileSdk androidVersions.compileSdkVersion

    defaultConfig {
        minSdkVersion androidVersions.minSdkVersion
        targetSdkVersion androidVersions.targetSdkVersion

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        debug {
            consumerProguardFiles "${projectDir}/proguard/consumer-proguard-autocomplete.txt"
        }
        release {
            minifyEnabled false
            consumerProguardFiles "${projectDir}/proguard/consumer-proguard-autocomplete.txt"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        allWarningsAsErrors = !project.hasProperty('android.injected.invoked.from.ide')
        jvmTarget = JavaVersion.VERSION_1_8.toString()

        /**
         * Migrate to Groovy syntax when the issue https://youtrack.jetbrains.com/issue/KT-37652 is resolved
         *
         *     kotlin {
         *         explicitApi()
         *     }
         */
        freeCompilerArgs += [
                '-Xexplicit-api=strict',
        ]
    }

    lint {
        abortOnError true
        disable 'ParcelCreator', 'ContentDescription'
    }
}

dependencies {
    implementation project(path: ':base')
    implementation project(path: ':sdk-common')

    implementation dependenciesList.kotlinCoroutines

    implementation dependenciesList.androidxCore
    implementation dependenciesList.androidxAppcompat

    ktlint dependenciesList.ktlint

    testImplementation dependenciesList.junit
    testImplementation dependenciesList.mockitoCore

    testImplementation dependenciesList.mockk

    testImplementation dependenciesList.jupiterApi
    testImplementation dependenciesList.jupiterEngine
    testImplementation project(":common-tests")

    androidTestImplementation dependenciesList.androidxTestRunner
    androidTestImplementation dependenciesList.androidxJunit
    androidTestImplementation dependenciesList.androidXEspressoCore
    androidTestImplementation dependenciesList.okHttpMockWebServer
    androidTestImplementation project(':common-tests')
}
